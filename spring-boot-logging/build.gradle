plugins {
    id "java"
    id "build-dashboard"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "org.springframework.boot" version "2.1.7.RELEASE"
    id "com.dorongold.task-tree" version "1.4"
    id "com.github.ben-manes.versions" version "0.22.0"
    id "se.patrikerdes.use-latest-versions" version "0.2.12"
}

// TODO: What's the point if it's in the "plugins" block?
apply plugin: "io.spring.dependency-management"

group = "x"
version = "0.0.1-SNAPSHOT"

sourceCompatibility = 11
targetCompatibility = 11

ext {
    logbookVersion = "1.13.0"
    logstashLogbackVersion = "6.1"
    problemSpringWebVersion = "0.24.0"
    springCloudVersion = "Greenwich.SR2"
}

ext["aspectj.version"] = "1.9.4"
ext["assertj.version"] = "3.13.2"
// TODO: Junit5 5.5.1 -- something more is needed by Gradle
ext["junit-jupiter.version"] = "5.4.2"
ext["lombok.version"] = "1.18.8"
ext["mockito.version"] = "2.27.2" // TODO: 3.0.0

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    all*.exclude module: 'junit'
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    compileOnly "org.projectlombok:lombok"
    compileOnly "org.springframework.boot:spring-boot-configuration-processor"

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.cloud:spring-cloud-starter-sleuth"
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign"
    implementation "org.zalando:logbook-spring-boot-starter"
    // TODO: Hrm ... too many dependencies for problems
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.zalando:problem-spring-web:${problemSpringWebVersion}"
    implementation "org.springframework.boot:spring-boot-starter-data-jdbc"

    testAnnotationProcessor "org.projectlombok:lombok"

    testCompileOnly "org.projectlombok:lombok"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.mockito:mockito-junit-jupiter"
    testImplementation "org.assertj:assertj-core"
    testImplementation "io.zonky.test:embedded-database-spring-test:1.5.0"

    runtimeOnly "net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}"
    runtimeOnly "org.postgresql:postgresql:42.2.6"
    runtimeOnly "org.flywaydb:flyway-core"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "org.zalando:logbook-bom:${logbookVersion}"
    }
}

test {
    failFast = true

    systemProperty "java.util.logging.config.file", "${project.rootDir}/config/logging.properties"

    testLogging {
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
    }

    useJUnitPlatform()

    testLogging {
        showExceptions = true
        showStandardStreams = true
    }
}

bootRun {
    systemProperties = System.properties as Map<String, ?>
}

springBoot {
    buildInfo()
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "5.6.1"
}

defaultTasks 'build'
