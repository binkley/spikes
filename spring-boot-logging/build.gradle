plugins {
    id "maven"
    id "java"
    id "build-dashboard"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    // TODO: All the deps are STUCK on 2.1.2 of Spring Boot
    id "org.springframework.boot" version "2.1.8.RELEASE"
    id "com.dorongold.task-tree" version "1.4"
    id "com.github.ben-manes.versions" version "0.22.0"
    id "se.patrikerdes.use-latest-versions" version "0.2.12"
    id "com.gorylenko.gradle-git-properties" version "2.1.0"
}

group = "x"
version = "0-SNAPSHOT"

sourceCompatibility = 11
targetCompatibility = 11

ext {
    javassistVersion = "3.25.0-GA"
    joptsimpleVersion = "5.0.4"
    jsqlparserVersion = "3.0"
    logbookVersion = "1.13.0"
    logstashLogbackVersion = "6.2"
    problemSpringWebVersion = "0.25.0"
    springCloudVersion = "Greenwich.SR2"
}

ext["aspectj.version"] = "1.9.4" // TODO: SB 2.1.8?
ext["assertj.version"] = "3.13.2" // TODO: SB 2.1.8?
ext["flyway.version"] = "5.2.4" // TODO: 6.0.+
ext["junit-jupiter.version"] = "5.4.2" // TODO: 5.5.+
ext["lombok.version"] = "1.18.8" // TODO: SB 2.1.8?
ext["micrometer.version"] = "1.2.0"
ext["mockito.version"] = "2.28.2" // TODO: 3.0.+

configurations {
    all {
        resolutionStrategy {
            if (false) failOnVersionConflict() // TODO: Lots of conflicts

            // Spring Cloud Greenwich.SR2 downgrades Spring Boot
            eachDependency { details ->
                if (details.requested.group == "org.springframework.boot")
                    details.useVersion "2.1.8.RELEASE"
            }
        }
    }

    compileOnly {
        extendsFrom annotationProcessor
    }

    all*.exclude module: "junit"
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    compileOnly "org.projectlombok:lombok"
    compileOnly "org.springframework.boot:spring-boot-configuration-processor"

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "io.micrometer:micrometer-registry-prometheus"
    implementation "org.springframework.cloud:spring-cloud-starter-sleuth"
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign"
    implementation "org.zalando:logbook-spring-boot-starter"
    implementation "com.github.jsqlparser:jsqlparser:${jsqlparserVersion}"
    implementation "net.sf.jopt-simple:jopt-simple:${joptsimpleVersion}"

    // TODO: Hrm ... too many dependencies for problems
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.zalando:problem-spring-web:${problemSpringWebVersion}"
    implementation "org.springframework.boot:spring-boot-starter-data-jdbc"

    testAnnotationProcessor "org.projectlombok:lombok"

    testCompileOnly "org.projectlombok:lombok"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.mockito:mockito-junit-jupiter"
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.javassist:javassist:${javassistVersion}"

    runtimeOnly "net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}"
    runtimeOnly "com.h2database:h2:1.4.199"
    runtimeOnly "org.flywaydb:flyway-core"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "org.zalando:logbook-bom:${logbookVersion}"
    }
}

test {
    failFast = true

    systemProperty "java.util.logging.config.file", "${project.rootDir}/config/logging.properties"

    testLogging {
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
    }

    useJUnitPlatform()

    testLogging {
        showExceptions = true
        showStandardStreams = true
    }
}

bootRun {
    systemProperties = System.properties as Map<String, ?>
}

springBoot {
    buildInfo()
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "5.6.2"
}

defaultTasks "build"
