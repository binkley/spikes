plugins {
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "java"
    id "net.ltgt.apt-idea" version "0.21"
    id "com.github.johnrengelman.shadow" version "4.0.2"
    id "application"
    id "net.ltgt.errorprone" version "0.8.1"
}

version "0.1"
group "hello.world"

ext {
    errorproneVersion = "2.3.3"
    handlebarsVersion = "4.1.2"
    logbackVersion = "1.2.3"
    lombokVersion = "1.18.8"
}

repositories {
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

dependencyManagement {
    imports {
        mavenBom "io.micronaut:micronaut-bom:1.2.0.RC1"
    }
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly
}

dependencies {
    errorprone "com.google.errorprone:error_prone_core:${errorproneVersion}"

    // Lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    annotationProcessor "io.micronaut:micronaut-inject-java"
    compile "io.micronaut:micronaut-inject"

    // Validation
    annotationProcessor "io.micronaut:micronaut-validation"
    compile "io.micronaut:micronaut-validation"
    // TODO: Why?
    compile "io.micronaut.configuration:micronaut-hibernate-validator"

    compile "io.micronaut:micronaut-runtime"
    compile "io.micronaut:micronaut-http-client"
    compile "javax.annotation:javax.annotation-api"
    compile "io.micronaut:micronaut-http-server-netty"

    // Admin endpoints
    compile "io.micronaut:micronaut-management"

    // Logback
    runtime "ch.qos.logback:logback-classic:${logbackVersion}"

    // Swagger
    compile "io.micronaut:micronaut-views"
    runtime "com.github.jknack:handlebars:${handlebarsVersion}"
    annotationProcessor "io.micronaut.configuration:micronaut-openapi"
    compile "io.swagger.core.v3:swagger-annotations"

    // Tracing
    compile "io.micronaut:micronaut-tracing"
    compile "io.opentracing.brave:brave-opentracing"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"

    // JUnit 5
    testCompile "org.junit.jupiter:junit-jupiter-api"
    testCompile "io.micronaut.test:micronaut-test-junit5"
    testRuntime "org.junit.jupiter:junit-jupiter-engine"
}

test.classpath += configurations.developmentOnly

mainClassName = "hello.world.Application"

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.errorprone.disableWarningsInGeneratedCode = true
    options.encoding = "UTF-8"
    options.compilerArgs.add("-parameters")
}

shadowJar {
    mergeServiceFiles()
}

run.classpath += configurations.developmentOnly
run.jvmArgs("-noverify", "-XX:TieredStopAtLevel=1", "-Dcom.sun.management.jmxremote")

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "5.5"
}
