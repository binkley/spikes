plugins {
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "build-dashboard"
    id "java"
    id "application"
    id "com.github.johnrengelman.shadow" version "4.0.2"
    id "net.ltgt.apt-idea" version "0.21"
}

version "0.1"
group "hello.world"

ext {
    h2Version = "1.4.199"
    handlebarsVersion = "4.1.2"
    logbackVersion = "1.2.3"
    lombokVersion = "1.18.8"
    micronautDataVersion = "1.0.0.M1"
}

repositories {
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

dependencyManagement {
    imports {
        mavenBom "io.micronaut:micronaut-bom:1.2.2"
    }
}

dependencies {
    // Lombok
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"

    annotationProcessor "io.micronaut:micronaut-inject-java"
    implementation "io.micronaut:micronaut-inject"

    implementation "io.micronaut:micronaut-validation"

    implementation "io.micronaut:micronaut-runtime"
    implementation "io.micronaut:micronaut-http-client"
    implementation "javax.annotation:javax.annotation-api"
    implementation "io.micronaut:micronaut-http-server-netty"

    // Persistence
    annotationProcessor "io.micronaut.data:micronaut-data-processor:${micronautDataVersion}"
    implementation "io.micronaut.data:micronaut-data-jdbc:${micronautDataVersion}"
    implementation "io.micronaut.configuration:micronaut-flyway"
    runtime "io.micronaut.configuration:micronaut-jdbc-hikari"
    runtime "com.h2database:h2:${h2Version}"

    // Admin endpoints
    implementation "io.micronaut:micronaut-management"

    // Logback
    runtime "ch.qos.logback:logback-classic:${logbackVersion}"

    // Swagger
    implementation "io.micronaut:micronaut-views"
    runtime "com.github.jknack:handlebars:${handlebarsVersion}"
    annotationProcessor "io.micronaut.configuration:micronaut-openapi"
    implementation "io.swagger.core.v3:swagger-annotations"

    // Tracing
    implementation "io.micronaut:micronaut-tracing"
    implementation "io.opentracing.brave:brave-opentracing"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"

    // JUnit 5
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testRuntime "org.junit.jupiter:junit-jupiter-engine"
}

mainClassName = "hello.world.Application"

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs.add("-parameters")
}

shadowJar {
    mergeServiceFiles()
}

run.jvmArgs("-noverify", "-XX:TieredStopAtLevel=1", "-Dcom.sun.management.jmxremote")

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "5.6.2"
}
